// ===----------------------------------------------------------------------===//
// Copyright Â© 2024 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//	https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
// ===----------------------------------------------------------------------===//
@Unlisted
module pkl.python.internal.PythonNamespace

import "pkl:reflect"
import "ClassGen.pkl"
import "EnumGen.pkl"
import "TypeAliasGen.pkl"
import "PythonMapping.pkl"
import "Gen.pkl"
import "utils.pkl"

`module`: reflect.Module

/// All mappings used by the module, including imports.
mappings: List<PythonMapping>

/// The name of the Pkl module.
pklModuleName: String

/// The mappings originate from this module.
moduleMappings: List<PythonMapping>

/// The Python namespace in which to place these mappings.
namespaceName: String

indent: String

local function describeLocation(src: reflect.TypeDeclaration) =
  let (memberType =
    if (src is reflect.Class && src.enclosingDeclaration.moduleClass == src) "module"
    else if (src is reflect.Class) "class"
    else "typealias"
  )
    "* \(memberType) `\(src.reflectee)` (\(src.location.displayUri))"

local function hasUniqueNames(): Boolean =
  let (names = moduleMappings.map((it) -> it.name))
    if (names.isDistinct) true
    else
      let (duplicateNames = moduleMappings.filter((it) -> moduleMappings.count((m) -> m.name == it.name) > 1))
      let (locations = duplicateNames.map((it) -> describeLocation(it.source)).join("\n"))
        throw("""
          Conflict: multiple Pkl declarations compute to Python name `\(duplicateNames.first.name)`.

          To resolve this conflict, add a `@python.Name` annotation to any of the following declarations:

          \(locations)

          For example:

          ```
          @python.Name { value = "CrabCakes" }
          class Crab_Cakes
          ```
          """)

local generated: List<Gen>(hasUniqueNames()) = moduleMappings.map((it) ->
  if (it is PythonMapping.Class)
    new ClassGen {
      indent = module.indent
      mappings = module.mappings
      mapping = it
      namespaceName = it.namespaceName
    }
  else if (it is PythonMapping.Enum)
    new EnumGen {
      indent = module.indent
      mappings = module.mappings
      mapping = it
      namespaceName = it.namespaceName
    }
  else
    new TypeAliasGen {
      indent = module.indent
      mappings = module.mappings
      mapping = it
      namespaceName = it.namespaceName
    }
)

local topLevelContents = generated.mapNonNull((g) -> g.topLevelContents).distinct.filter((it) -> !(it.isEmpty))

/*
local convenienceLoaders = """
  /// Load the Pkl module at the given source and evaluate it into `\(namespaceName).Module`.
  ///
  /// - Parameter source: The source of the Pkl module.
  static func loadFrom(source: ModuleSource) async throws -> \(namespaceName).Module {
  \(module.indent)try await PklPython.withEvaluator { evaluator in
  \(module.indent.repeat(2))try await loadFrom(evaluator: evaluator, source: source)
  \(module.indent)}
  }

  /// Load the Pkl module at the given source and evaluate it with the given evaluator into
  /// `\(namespaceName).Module`.
  ///
  /// - Parameter evaluator: The evaluator to use for evaluation.
  /// - Parameter source: The module to evaluate.
  def load_from(
  \(module.indent)evaluator: PklPython.Evaluator,
  \(module.indent)source: PklPython.ModuleSource
  ) async throws -> \(namespaceName).Module {
  \(module.indent)try await evaluator.evaluateModule(source: source, as: Module.self)
  }
  """
*/
local convenienceLoaders = """
  @classmethod
  def load_pkl(cls, source: str):
  \(module.indent)# Load the Pkl module at the given source and evaluate it into `\(namespaceName).Module`.
  \(module.indent)# - Parameter source: The source of the Pkl module.
  \(module.indent)config = pkll.load(source)
  \(module.indent)return cls(**config._asdict())
  """

local generatedPartitioned = generated.partition((it)-> if(it is ClassGen) utils.isModuleClass(it.clazz) else false)

contents = new Listing {
  utils.renderHeaderComment(`module`)
  """
  from typing import Dict, List, Literal, Optional, Union
  from dataclasses import dataclass
  import pkll
  """
  when (topLevelContents.length > 0) {
    topLevelContents.join("\n")
  }
  ""
  "_NO_DEFAULT = object()"
  //"# class \(namespaceName):\n"
  /*
  for (gen in generated.second) {
    when (gen != generated.first) {
      ""
    }
    //module.indent + gen.contents.replaceAll(Regex("\n(?!\n)"), "\n\(module.indent)")
    //gen.contents.replaceAll(Regex("\n(?!\n)"), "\n\(module.indent)")
    gen.contents
  }
  */
  ""
  for (gen in generatedPartitioned.second) {
    /*
    when (gen != generatedPartitioned.second.first) {
      ""
    }
    */
    gen.contents
    ""
  }
  for (gen in generatedPartitioned.first) {
    gen.contents // moduleClass goes last
  }
  when (!`module`.modifiers.contains("open") && !`module`.modifiers.contains("abstract")) {
    ""
    module.indent + convenienceLoaders.replaceAll(Regex("\n(?!\n)"), "\n\(module.indent)")
  }
}.join("\n")

output {
  files {
    ["\(namespaceName)_pkl.py"] {
      text = contents
    }
  }
}
